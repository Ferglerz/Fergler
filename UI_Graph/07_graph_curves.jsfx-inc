// UI Graph Curves Module - Curve rendering functions for compression graph visualization
// Depends on: UI_General/00_ui_constants.jsfx-inc, UI_General/02_ui_utils.jsfx-inc, UI_General/12_coordinate_conversion.jsfx-inc, 03_Compression/02_graph_data_core.jsfx-inc, 03_Compression/03_graph_curves.jsfx-inc, UI_General/01_drawing_primitives.jsfx-inc, UI_Graph/06_graph_cache.jsfx-inc, UI_General/11_threshold_lines.jsfx-inc

@init

lut_viz_cache_dirty = 1;
lut_viz_cache_points = 0;
lut_viz_cache_size = 0;

function draw_compression_lut_curve() (
  comp_lut_dirty ? build_compression_lut();
  gfx_set(0.2, 0.8, 1.0, 1);
  first_point = 1;
  end_index = floor((GRAPH_MAX_DB - COMP_LUT_MIN_DB) / COMP_LUT_GRANULARITY);
  lut_idx = 0;
  while (lut_idx <= end_index) (
    input_db = COMP_LUT_MIN_DB + (lut_idx * COMP_LUT_GRANULARITY);
    output_db = comp_lut[lut_idx];
    screen_x = db_to_graph_x(input_db);
    screen_y = db_to_graph_y(output_db);
    
    first_point ? (
      gfx_x = screen_x;
      gfx_y = screen_y;
      first_point = 0;
    ) : (
      gfx_lineto(screen_x, screen_y, 1);
    );
    
    lut_idx += 1;
  );
);

function draw_debug_invisible_points(point_index, curve_factor) (
  menu_debug_enabled ? (
    gfx_set(1, 0, 0, 0.8);
    pt_idx = point_index * 2;
    prev_idx = (point_index - 1) * 2;
    next_idx = (point_index + 1) * 2;
    point_x_db = graph_points[pt_idx];
    point_y_db = graph_points[pt_idx + 1];
    prev_x_db = graph_points[prev_idx];
    prev_y_db = graph_points[prev_idx + 1];
    next_x_db = graph_points[next_idx];
    next_y_db = graph_points[next_idx + 1];
    gfx_circle(db_to_graph_x(point_x_db + (prev_x_db - point_x_db) * curve_factor), db_to_graph_y(point_y_db + (prev_y_db - point_y_db) * curve_factor), 1.5, 1, 1);
    gfx_circle(db_to_graph_x(point_x_db + (next_x_db - point_x_db) * curve_factor), db_to_graph_y(point_y_db + (next_y_db - point_y_db) * curve_factor), 1.5, 1, 1);
  );
);


function draw_mixed_curves() local(i) (
  debug_counter_ui_curves += 1;
  menu_debug_enabled ? (
    draw_compression_lut_curve();
  ) : (
    cache_curve_if_needed();
    gfx_set(1, 0.8, 0.2, 1);
    i = 0;
    while (i < curve_cache_size) (
      seg_idx = i * 4;
      gfx_x = curve_cache_points[seg_idx];
      gfx_y = curve_cache_points[seg_idx + 1];
      gfx_lineto(curve_cache_points[seg_idx + 2], curve_cache_points[seg_idx + 3], 1);
      i += 1;
    );
  );
  menu_debug_enabled ? (
    i = 1;
    while (i < num_points - 1) (
      curve_amount = get_curve_amount(i);
      curve_amount > 0 ? draw_debug_invisible_points(i, curve_amount * 0.01);
      i += 1;
    );
  );
);

function draw_graph_points() (
  menu_debug_enabled ? (
    corner_x = db_to_graph_x(graph_points[0]);
    corner_y = db_to_graph_y(graph_points[1]);
    gfx_set(0.5, 0.5, 1.0, 0.8);
    gfx_circle(corner_x, corner_y, 3, 1, 1);
    p1_x = db_to_graph_x(graph_points[2]);
    p1_y = db_to_graph_y(graph_points[3]);
    gfx_set(0.5, 0.5, 1.0, 0.4);
    gfx_line(corner_x, corner_y, p1_x, p1_y, 1);
    last_idx = num_points - 1;
    last_idx2 = last_idx * 2;
    corner_x = db_to_graph_x(graph_points[last_idx2]);
    corner_y = db_to_graph_y(graph_points[last_idx2 + 1]);
    gfx_set(0.5, 0.5, 1.0, 0.8);
    gfx_circle(corner_x, corner_y, 3, 1, 1);
    prev_idx2 = (last_idx - 1) * 2;
    p_last_x = db_to_graph_x(graph_points[prev_idx2]);
    p_last_y = db_to_graph_y(graph_points[prev_idx2 + 1]);
    gfx_set(0.5, 0.5, 1.0, 0.4);
    gfx_line(p_last_x, p_last_y, corner_x, corner_y, 1);
  );
  i = 1;
  while (i < num_points - 1) (
    pt_idx = i * 2;
    input_db = graph_points[pt_idx];
    output_db = graph_points[pt_idx + 1];
    point_x = db_to_graph_x(input_db);
    point_y = db_to_graph_y(output_db);
    is_hovered = (i == hovered_point);
    is_dragging = (i == dragging_point);
    curve_amount = get_curve_amount(i);
    is_curved = curve_amount > 0;
    point_radius = is_hovered ? 6 : 4;
    set_point_color(is_hovered, is_curved);
    gfx_circle(point_x, point_y, point_radius, 1, 1);
    is_curved ? (
      gfx_set(1, 0.8, 0.2, 0.6);
      gfx_circle(point_x, point_y, point_radius + 2, 0, 1);
    );
    is_dragging && mouse_down ? (
      text_x = point_x + 15;
      text_y = point_y;
      draw_text_at(1, 1, 1, 1, text_x, text_y - 4, "%.1f dB", output_db - input_db);
      draw_text_at(1, 1, 0.6, 1, text_x, text_y + 8, "%.1f dB", output_db);
      is_curved ? draw_text_at(1, 1, 1, 1, text_x, text_y + 20, "Curve: %.0f%%", curve_amount);
    );
    i += 1;
  );
);


function draw_threshold_lines_on_graph() (
  render_input_level_threshold_line(GRAPH_X, GRAPH_Y, GRAPH_SIZE);
);

