// UI Graph Cache Module - Curve caching system for maximum rendering efficiency
// Depends on: UI_General/00_ui_constants.jsfx-inc, UI_General/02_ui_utils.jsfx-inc, UI_General/12_coordinate_conversion.jsfx-inc, 03_Compression/02_graph_data_core.jsfx-inc, 03_Compression/03_graph_curves.jsfx-inc

@init

curve_cache_dirty = 1;
grid_cache_dirty = 1;
curve_cache_points_allocated = 0;
curve_cache_size = 0;
curve_cache_hash = 0;
curve_cache_points = 0;

function calculate_curve_hash() (
  hash = 0;
  i = 0;
  while (i < num_points) (
    hash = hash * 31 + graph_points[i*2] * 1000;
    hash = hash * 31 + graph_points[i*2 + 1] * 1000;
    hash = hash * 31 + curve_amounts[i] * 100;
    i += 1;
  );
  hash = hash * 31 + strength_multiplier * 1000;
  hash = hash * 31 + num_points;
  hash < 0 ? hash = -hash;
  hash;
);

function generate_curve_cache() (
  !curve_cache_points_allocated ? (
    curve_cache_points = freemem;
    freemem += GRAPH_SIZE * 4;
    curve_cache_points_allocated = 1;
  );
  segment_count = 0;
  num_points >= 2 ? (
    prev_x = clamp_x_to_graph(db_to_graph_x(graph_points[0]), GRAPH_X, GRAPH_SIZE);
    prev_y = clamp_y_to_graph(db_to_graph_y(graph_points[1]), GRAPH_Y, GRAPH_SIZE);
    clamped_junction_x = 0;
    clamped_junction_y = 0;
    use_clamped_junction = 0;

    i = 0;
    while (i < num_points - 1) (
      idx = i + 1;
      curve_amt = (idx > 0 && idx < num_points - 1) ? get_curve_amount(idx) : 0;
      curve_amt > 0 ? (
        calculate_bezier_control_points(idx, curve_amt);
        
        p0_x = db_to_graph_x(bezier_p0_x);
        p0_y = db_to_graph_y(bezier_p0_y);
        p1_x = db_to_graph_x(bezier_control1_x);
        p1_y = db_to_graph_y(bezier_control1_y);
        p2_x = db_to_graph_x(bezier_control2_x);
        p2_y = db_to_graph_y(bezier_control2_y);
        p3_x = db_to_graph_x(bezier_p3_x);
        p3_y = db_to_graph_y(bezier_p3_y);
        use_clamped_junction ? (
          p0_x = clamped_junction_x;
          p0_y = clamped_junction_y;
          use_clamped_junction = 0;
        );
        next_idx = i + 2;
        (next_idx > 0 && next_idx < num_points - 1) ? (
          next_curve_amount = get_curve_amount(next_idx);
          next_curve_amount > 0 ? (
            calculate_bezier_control_points(next_idx, next_curve_amount);
            next_p0_x = db_to_graph_x(bezier_p0_x);
            next_p0_y = db_to_graph_y(bezier_p0_y);
            p3_x > next_p0_x ? (
              clamped_junction_x = (p3_x + next_p0_x) * 0.5;
              clamped_junction_y = (p3_y + next_p0_y) * 0.5;
              p3_x = clamped_junction_x;
              p3_y = clamped_junction_y;
              use_clamped_junction = 1;
            );
          );
        );
        
        t = 0;
        t_step = 1.0 / BEZIER_STEPS;
        while (t < 1 && segment_count < GRAPH_SIZE - 1) (
          u = 1 - t;
          tt = t * t;
          uu = u * u;
          current_x = clamp_x_to_graph(uu * u * p0_x + 3 * uu * t * p1_x + 3 * u * tt * p2_x + tt * t * p3_x, GRAPH_X, GRAPH_SIZE);
          current_y = clamp_y_to_graph(uu * u * p0_y + 3 * uu * t * p1_y + 3 * u * tt * p2_y + tt * t * p3_y, GRAPH_Y, GRAPH_SIZE);
          seg_idx = segment_count * 4;
          curve_cache_points[seg_idx] = prev_x;
          curve_cache_points[seg_idx + 1] = prev_y;
          curve_cache_points[seg_idx + 2] = current_x;
          curve_cache_points[seg_idx + 3] = current_y;
          segment_count += 1;
          prev_x = current_x;
          prev_y = current_y;
          t += t_step;
        );
      ) : (
        end_x = clamp_x_to_graph(db_to_graph_x(graph_points[idx*2]), GRAPH_X, GRAPH_SIZE);
        end_y = clamp_y_to_graph(db_to_graph_y(graph_points[idx*2 + 1]), GRAPH_Y, GRAPH_SIZE);
        seg_idx = segment_count * 4;
        curve_cache_points[seg_idx] = prev_x;
        curve_cache_points[seg_idx + 1] = prev_y;
        curve_cache_points[seg_idx + 2] = end_x;
        curve_cache_points[seg_idx + 3] = end_y;
        segment_count += 1;
        prev_x = end_x;
        prev_y = end_y;
      );
      
      i += 1;
    );
  );
  
  curve_cache_size = segment_count;
  curve_cache_dirty = 0;
  curve_cache_hash = calculate_curve_hash();
  comp_lut_dirty ? build_compression_lut();
);

function cache_curve_if_needed() (
  curve_cache_dirty || curve_cache_hash != calculate_curve_hash() ? generate_curve_cache();
);

